rootProject.name = 'osh-addons'
gradle.ext.oshCoreDir = "$rootDir/../osh-core"

if (!new File(gradle.oshCoreDir).exists())
    throw new GradleException('Repository osh-core is missing. Please clone it from https://github.com/opensensorhub/osh-core')

// include osh-core build only if not part of larger build
// this is needed to avoid nested composite builds
// comment this to build against published artifacts
if (gradle.parent == null)
  includeBuild gradle.oshCoreDir

// include all folders with build.gradle as subprojects
FileTree subprojects = fileTree(rootDir).exclude('core', 'dist').include('**/build.gradle')
subprojects.files.each { File f ->
  File projectFolder = f.parentFile
  if (projectFolder != rootDir) {
    String projectName = ':' + projectFolder.name
    //String projectName = ':' + rootDir.toPath().relativize(projectFolder.toPath()).toString().replace('/', ':')
    include projectName
    project(projectName).projectDir = projectFolder
  }
}
