apply from: gradle.oshCoreDir + '/common.gradle'
version = oshCoreVersion
description = 'OSH Add-ons'


subprojects {
  version = oshCoreVersion
  
  // inject all repositories from included builds if any
  repositories.addAll(rootProject.repositories)
  
  // add project dependency to proper configuration depending on version
  // this is used to build different zip distributions
  afterEvaluate { p ->
    rootProject.dependencies {
      def dep = p.version.endsWith('SNAPSHOT') ? dev(p) : stable(p)
      dep.exclude group: 'org.sensorhub'
      dep.exclude group: 'org.vast.opengis'
    }
  }
}


// create one configuration for each distribution
configurations {
  stable
  dev
}
 
  
// distributions content
distributions {
  dev {
    baseName = 'osh-addons-dev'
    contents {
      into('lib') {
        from project.configurations.dev
      } 
    }
  }

  stable {
    baseName = 'osh-addons-stable'
    contents {
      into('lib') {
        from project.configurations.stable
      } 
    }
  }
}

apply from: gradle.oshCoreDir + '/release.gradle'
